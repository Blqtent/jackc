/*
 * 			    Abstract Syntax Tree
 *
 *                      17 april MMXXI PUBLIC DOMAIN
 *           The author disclaims copyright to this source code.
 *
 *
 */

class JackAst {
	field int tag;
	field String data;
	field JackAst left;	
	field JackAst right;	
	field JackAst child;
	field JackAst next;
	field JackAst parent;	
	field int line;	
	field String file;	

	constructor JackAst new (int tag_, String data_, JackAst parent_) {
		let tag = tag_;
		let data = data_.copy();
		let parent = parent_;
		let child = null;
		let next = null;
		let left = null;
		let right = null;
		let line = 0;
		let file = "";
		return this;
	}
	
	method void dispose() {
		var JackAst c, d;

		do data.dispose();
		let c = child;
		while (c) {
			let d = c.getNext();	
			do c.dispose();
			let c = d;
		}
		if (left) {
			do left.dispose();
		}
		if (right) {
			do right.dispose();
		}
		do Memory.deAlloc(this);
		return;
	}

	method void setLine(int line_, String file_) {
		let line = line_;
		let file = file_;
		return;
	}

	method void ident(File f, int level) {
		while (level > 0) {
			let level = level - 1;
			do f.writeString("  ");
		}
		return;			
	}
	method void process(File f, int level) {
		var JackAst c, d;
		var String s;
		var int i;
		let s = String.new(8);
		do ident(f, level);
		let s = s.setInt(tag);
		do f.writeString("tag=");
		do f.writeString(s);
		do s.dispose();
		do f.writeString(" data=");
		do f.writeString(data);
		do f.writeByte(10);
		let level = level + 1;
		if (left) {
			do ident(f, level);
			do f.writeString("left:");
			do f.writeByte(10);
			do left.process(f, level + 1);
		}
		if (right) {
			do ident(f, level);
			do f.writeString("right:");
			do f.writeByte(10);
			do right.process(f, level + 1);
		}
		let c = child;
		while (c) {
			do c.process(f, level);
			let d = c.getNext();	
			let c = d;
		}
		return;
	}


	method JackAst add(int tag_, String data_) {
		var JackAst a;
		let a = JackAst.new(tag_, data_, this);
		do append(a);
		return a;
	}

	method JackAst addop(String data_, JackAst left_) {
		var JackAst a;
		let a = JackAst.new(C.OP(), data_, this);
	 	do a.setLeft(left_);	
		return a;
	}

	method JackAst set(int tag_, String data_) {
		var JackAst c, d;
		let c = child;
		while (c) {
			if ((c.getTag() = tag_) & (~data_.compare(c.getData())))
			{
				return c;
			}
			let c = c.getNext();	
		}
		return  add(tag_, data_);
	}


	method JackAst append(JackAst a) {
		var JackAst c, d;
		let c = child;
		if (c = null) {
			let child = a;
		} else {
			while (c.getNext()) {
				let c = c.getNext();
			}
			do c.setNext(a);
		}
		return a;
	}

	method JackAst pop() {
		var JackAst c, d;
		let c = child;
		if (c = null) {
			return null;
		} else {
			let d = c;
			let child = c.getNext();
		}
		return d;
	}

	method void setLeft(JackAst n) {
		let left = n;
		return;
	}

	method void setRight(JackAst n) {
		let right = n;
		return;
	}


	method void setNext(JackAst n) {
		let next = n;
		return;
	}

	method int getTag() {
		return tag;
	}


	method String getData() {
		return data;
	}

	method JackAst getChild() {
		return child;
	}

	method JackAst getRight() {
		return right;
	}
	method JackAst getLeft() {
		return next;
	}
	method JackAst getParent() {
		return parent;
	}
	method JackAst getNext() {
		return next;
	}
}

