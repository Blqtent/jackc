
class JackParser {
	//field Xml dst;
	
	field Xml x;
	field JackTokenizer jt;
	field File src;
	field int maxerror;
	field int nberror;
	field String exp;
	field boolean gen_xml;
	field int line;
	field int last_line;
	field Ast ast;
	field Ast current;
	constructor JackParser new(JackTokenizer source) 
	{
		let nberror = 0;
		let maxerror = 1;
		let src = null;
		let jt = source;
		let x = source.getXml();
		let gen_xml = true;

		let ast = Ast.new(C.ROOT(), "", null);
		let current = ast;

		return this;
	}

	method void dispose() {
		do ast.dispose();
		do Memory.deAlloc(this);
		return;
	}

	method void init(File source, boolean xml_output) {
		let src = source;
		let nberror = 0;
		let gen_xml = xml_output;
		let line = 0;
		let last_line = -1;
		do ast.dispose();
		let ast = Ast.new(C.ROOT(), "", null);
		let current = ast;
		return;
	}

	method Ast getAst() {
		return ast;
	}

	method boolean advance() {
		if (jt.hasMoreTokens()) {
			do jt.advance();
		} else {
			return false;
		}
		return true;
	}

	method void matched(int tok, int k) {
		var String s;
		return;
		do Output.printInt(tok);
		do Output.printString(" ");
		do Output.printInt(k);
		do Output.println();
		return;
	}
 
	method boolean matchKeyword(String k, boolean eat) {	
		var String t;
		if (eat) {
		 	if (jt.hasMoreTokens()) {
				do jt.advance();
			} else {
				return false;
			}
		}
		let t = jt.tokenType();

		if (t = C.KEYWORD()) {
			if (jt.keyWord() = k) {
				do matched(k, 0);
				return true;
			}
		}
		return false;
	}

	method boolean matchSymbol(char k, boolean eat) {	
		if (eat) {
			if (jt.hasMoreTokens()) {
				do jt.advance();
			} else {
				return false;
			}
		}
		if (jt.tokenType() = C.SYMBOL()) {
			if (jt.symbol() = k) {
				do matched(C.SYMBOL(), k);
				return true;
			}
		}
		return false;
	}

	method boolean matchIntegerConstant(boolean eat) {	
		if (eat) {
		 	if (jt.hasMoreTokens()) {
				do jt.advance();
			} else {
				return false;
			}
		}
		if (jt.tokenType() = C.INT_CONST()) {
			do matched(C.INT_CONST(), 0);
			return true;
		}
		return false;
	}

	method boolean matchStringConstant(boolean eat) {	
		if (eat) {
		 	if (jt.hasMoreTokens()) {
				do jt.advance();
			} else {
				return false;
			}
		}
		if (jt.tokenType() = C.STRING_CONST()) {
			do matched(jt.stringVal(), 0);
			return true;
		}
		return false;
	}
	
	method boolean matchIdentifier(boolean eat) {	
		if (eat) {
		 	if (jt.hasMoreTokens()) {
				do jt.advance();
			} else {
				return false;
			}
		}
		if (jt.tokenType() = C.IDENTIFIER()) {
			do matched(jt.identifier(), 0);
			return true;
		}
		return false;
	}
	
	method int error(String w) {
		var String s;

		let nberror = nberror + 1;
		if (nberror > maxerror) {
			return;
		}
		do Output.printString("Syntax error in ");
		do Output.printString(src.getName());
		do Output.printString(" ");
		do Output.printString(w);
		do Output.printString(" at line(");
		do Output.printInt(jt.getLine());
		do Output.printString(") got '");
		
		let s = getData();
		do Output.printString(s);
		do s.dispose();
		do Output.printString("'");
		do Output.println();
		return 0;
	}

	method String getData() { 
		var String s;
		let s = null;
		if (jt.tokenType() = C.KEYWORD()) {
			let s = jt.keyWord();
			let s = s.copy();
		}
		if (jt.tokenType() = C.SYMBOL()) {
			let s = String.new(3);
			let s = s.appendChar(jt.symbol());
		}
		if (jt.tokenType() = C.INT_CONST()) {
			let s = String.new(8);
			let s = s.setInt(jt.intVal());
		}
		if (jt.tokenType() = C.STRING_CONST()) {
			let s = jt.stringVal();
			let s = s.copy();
		}
		if (jt.tokenType() = C.IDENTIFIER()) {
			let s = jt.identifier();
			let s = s.copy();
		}
		if (jt.tokenType() = C.ASM()) {
			let s = jt.identifier();
			let s = s.copy();
		}
		if (jt.tokenType() = 0) {
			let s = "end of file";
			let s = s.copy();
		}
		if (s = null) {
			let s = String.new(1);
		}
		return s;
	}

	method void addLine() {
		var String u;
		if (jt.getLine() > last_line) {
			let last_line = jt.getLine();
			let u = String.new(8);
			let u = u.setInt(last_line);
			do x.addAttribute("line", u);
			do u.dispose();
		}
		return;
	}

	method void addStringConstant() {
		if (~gen_xml) {
			return;
		}
		do x.addElement(C.STRING_CONST());
		do x.addContent(jt.stringVal());
		do addLine();
		do x.closeCurrent();
		return;
	}

	method void addIntegerConstant() {
		var String s;
		if (~gen_xml) {
			return;
		}
		let s = String.new(8);
		let s = s.setInt(jt.intVal());
		do x.addElement(C.INT_CONST());
		do x.addContent(s);
		do s.dispose();
		do addLine();
		do x.closeCurrent();
		return;
	}
		
	method void addSymbol() {
		var String s;
		if (~gen_xml) {
			return;
		}
		do x.addElement(C.SYMBOL());
		let s = String.new(3);
		let s = s.appendChar(jt.symbol());
		do x.addContent(s);
		do s.dispose();
		do addLine();
		do x.closeCurrent();
		return;
	}

	method void addIdentifier() {
		if (~gen_xml) {
			return;
		}
		do x.addElement(C.IDENTIFIER());
		do x.addContent(jt.identifier());
		do addLine();
		do x.closeCurrent();
		return;
	}

	method void addKeyword() {
		if (~gen_xml) {
			return;
		}
		do x.addElement(C.KEYWORD());
		do x.addContent(jt.keyWord());
		do addLine();
		do x.closeCurrent();
		return;
	}
	
	method void begin(String nonTerminal) {
		if (~gen_xml) {
			return;
		}
		do x.addElement(nonTerminal);
	}

	method void end(String nonTerminal) {
		var XmlNode c;
		var String s;
		if (~gen_xml) {
			return;
		}
		do x.closeElement(nonTerminal);
		//do x.disposeCurrent();
/*
		let c = x.getCurrent();
		let s = c.getTag();
		while ((~(c.getParent()=null)) & s.compare(nonTerminal)) {
				
			do x.closeCurrent();
			let c = x.getCurrent();
			let s = c.getTag();
		}
*/
		return;		
	}

	method void astSetCurrent(Ast c) {
		let current = c; 
	}

	method void astAdd(int c) {
		var String s;
		let s = getData();
		let current = current.add(c, s);
		do s.dispose(); 
	}

	method void compileClass(JackTokenizer tk, File dst_) {
		var boolean doit;
 
		let jt = tk;

		let doit = true; 
		if (~matchKeyword(C.CLASS(), true)) {
			do error("Expected class");
			return;
		}
		if (~matchIdentifier(true)) {
			do error("Expected class identifier");
			return;
		}
		do astAdd(C.CLASS());

		if (~matchSymbol(C.LEFT_CURLY_BRACKET(), true)) {
			do error("Expected {");
			return;
		}
		do addSymbol();

		do advance();
		while (compileClassVarDec()) {
			let doit = doit; 
		}
		do compileCallbackDec();
		while (compileSubroutine()) {
			let doit = doit; 
		}
		if (~matchSymbol(C.RIGHT_CURLY_BRACKET(), false)) {
			do error("Expected } ");
			return;
		}	
		do addSymbol();
		if (~(jt.hasMoreTokens() = 0)) {
			do error("Extra content after class definition");
			return;
		}	
		return; // success
	}

	method boolean compileClassVarDec() {
		var boolean z;
		var Ast here;


		let z = matchKeyword(C.STATIC(), false);
		if (z) {
			do astAdd(C.STATIC());
		} else {
			let z = z | matchKeyword(C.FIELD(), false);
			if (z) {
				do astAdd(C.FIELD());
			}
		}
		if (~z) {	
			return false;
		}
		
		let z = matchKeyword(C.INT(), true); // type
		if (z) {
			do astAdd(C.INT());
		} else {
			let z = z | matchKeyword(C.CHAR(), false);
			if (z) {
				do astAdd(C.CHAR());
			} else {
				let z = z | matchKeyword(C.BOOLEAN(), false);
				if (z) {
					do astAdd(C.BOOLEAN());
				}
			}
		}
		if (z) {
			let z = z;
		} else {
			let z = z | matchIdentifier(false);
			if (z) {
				do astAdd(C.CLASSO());
			}
		}
		if (~z) { 
			do error("type expected");
			return false;
		}					
		let z = matchIdentifier(true); // varName
		if (~z) {
			do error("varName expected.");	
			return false;
		}				
	
		do astAdd(C.VARNAME());
	
		let z = matchSymbol(C.COMMA(), true); // ,
		while (z) {
 			// varName
			do addSymbol();
			let z = matchIdentifier(true);
			if (z) {
				do addIdentifier();
				let z = matchSymbol(C.COMMA(), true);
				if (~z) {
					let z = matchSymbol(C.SEMICOLON(), false);
					if (z) {
						do addSymbol();
						do advance();
						do end("classVarDec");
				  		return true;
					} else {
				  		do error(", or ; expected");
						return false;
					}
		    	    	}
			} else { 
				let z = matchSymbol(C.SEMICOLON(), false);
				if (z) {
					do addSymbol();
					do advance();
					do end("classVarDec");
					return true;
				}
				do error(",varName expected");
				return false;
			} 
		}
		let z = matchSymbol(C.SEMICOLON(), false);
		if (z) {
			do addSymbol();
			do advance();		
			do end("classVarDec");
			return true;
		} 
		do error("';' expected.");
		return false;
	}

	method boolean compileSubroutineBody() {
		var boolean z;
		let z = matchSymbol(C.LEFT_CURLY_BRACKET(), false);
		if (z) {
			do addSymbol();
			do advance();
			let z = compileVarDec();
			let z = compileStatements();
			let z = matchSymbol(C.RIGHT_CURLY_BRACKET(), false);
			if (z) {
				do addSymbol();
				do advance();
				return true;
			} 
			do error("'}' expected,,");
			return false;
		}
		do error("'{Í„' expected");
		return false;
	}

	method void compileCallbackDec() {
		var boolean z;
		let z = matchKeyword(C.CALLBACK(), false);
		if (~z) {
			return;
		}
		do begin("callbackDec");
		let z = matchKeyword(C.INT(), true); // type
		if (~z) {
			do error("'int' expected");
			return;
		}
		let z = matchKeyword(C.CALLBACK(), true); // type
		if (~z) {
			do error("'int' expected");
			return;
		}
		let z = matchSymbol(C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("expect '('");
			return false;
		}				
		do addSymbol();

		do advance();
		do compileParameter();
		let z = matchSymbol(C.COMMA(), false);
		if (~z) {
	        	do error("expect ',' ");
			return false;

		}
		do advance();
		do compileParameter();
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (z) {
			do addSymbol();
			do advance();
			let z = compileSubroutineBody();
			do end("callbackDec");
			return z;
	    	}
	        do error("expect ')'.");
		return false;
		
	}

	method boolean compileSubroutine() {
		var boolean z;
		let z = matchKeyword(C.CONSTRUCTOR(), false);
		let z = z | matchKeyword(C.FUNCTION(), false);
		let z = z | matchKeyword(C.METHOD(), false);
		if (~z) {
			return false;
		}
		do begin("subroutineDec");
		do addKeyword();

		let z = matchKeyword(C.VOID(), true); // type
		let z = z | matchKeyword(C.INT(), false);
		let z = z | matchKeyword(C.CHAR(), false);
		let z = z | matchKeyword(C.BOOLEAN(), false);
		if (z) {
			do addKeyword();
		} else {		
			let z = z | matchIdentifier(false);
			if (z) {
				do addIdentifier();
			}
		}
		if (~z) {
			do error("expect type");
			return false;
		}
		let z = matchIdentifier(true); // subroutinName
		if (~z) {
			do error("expect subroutineName");
			return false;
		}
		do addIdentifier();
			
		let z = matchSymbol(
			C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("expect '('");
			return false;
		}				
		do addSymbol();

		do advance();
		let z = compileParameterList();
		if (~z) {
			return false;
		}
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (z) {
			do addSymbol();
			do advance();
			let z = compileSubroutineBody();
			do end("subroutineDec");
			return z;
	    	}
	        do error("expect ')'.");
		return false;
	}

	method boolean compileParameter() {
		var boolean z;
		let z = matchKeyword(C.INT(), false); // type
		let z = z | matchKeyword(C.CHAR(), false);
		let z = z | matchKeyword(C.BOOLEAN(), false);
		if (z) {
			do addKeyword();
		} else {
			let z = z | matchIdentifier(false);
			if (z) {
				do addIdentifier();
			}
		}
		if (z) {
			let z = matchIdentifier(true); // varName
			if (z) {
				do addIdentifier();
				do advance();
				return true;
			} else {	
				do error("varName expected..");
				return false;
			}
		} else {
			return false;
		}
	}

	method boolean compileParameterList() {
		var boolean z;
		let z = true;
		do begin("parameterList");
		while (jt.hasMoreTokens()) {
			let z = compileParameter();
			let z = matchSymbol(C.COMMA(), false);
			if (~z) {
				do end("parameterList");
				return true;
			}
			do addSymbol();
			do advance();
		}
		return false;
	}

	method boolean compileVarDecVarList() {
		var boolean z;

		let z = matchIdentifier(true); // varName
		while (z) {
			do addIdentifier();
			let z = matchSymbol(C.COMMA(), true);
			if (~z) {
				let z = matchSymbol(C.SEMICOLON(), false);
				if (~z) {
				  	do error(", or ; expected");
				  	return false;	
				}
				do addSymbol();
				do advance();
				let z = false;
			} else {
			   	let z = matchIdentifier(true); //varName
			    	if (~z) {
			   		do error("varName expected..");
				  	return false;
			    	}				
			    	let z = true;
			}	
		} 	
		return true;
	}

	method boolean compileVarDec() {
		var boolean z;
		let z = true;
		let z = matchKeyword(C.VAR(), false); //
		if (~z) {
			return false;
		}
		while (z) {
			do begin("varDec"); 
			do addKeyword();
			let z = matchKeyword(C.INT(), true); // type
			let z = z | matchKeyword(C.CHAR(), false);
			let z = z | matchKeyword(C.BOOLEAN(), false);
			if (z) {
				do addKeyword();
			} else {
				let z = z | matchIdentifier(false);
				if (z) {
					do addIdentifier();
				}
			}
			if (z) {
				let z = compileVarDecVarList();
			}
			do end("varDec"); 
			let z = matchKeyword(C.VAR(), false);  
		}
		return true;
	}

	method boolean compileStatements() {
		var boolean z;
		let z = true;
		do begin("statements"); 
		while (true) {
			let z = matchSymbol(C.RIGHT_CURLY_BRACKET(), false);
			if (z) {
				do end("statements"); 
				return false;
			}
			if (matchKeyword(C.LET(), false)) {
				do compileLet();
			} else { if (matchKeyword(C.DO(), false)) {
				do compileDo();
			} else { if (matchKeyword(C.IF(), false)) {
				do compileIf();
			} else { if (matchKeyword(C.WHILE(), false)) {
				do compileWhile();
			} else { if (matchKeyword(C.RETURN(), false)) {
				do compileReturn();
			} else { if (jt.tokenType() = C.ASM()) {
				do advance();
			} else {
				do error("statement expected");
				return false;
			}}}}}}
		}
		return false;
	}

	method void compileDo() {
		var String s;
		var boolean z;
		var int l;

		if (~matchKeyword(C.DO(), false)) {
			do error("do expected");
			return;
		}
		do begin("doStatement"); 

		if (~matchIdentifier(true)) { // subroutineName
			do error("subroutineName expected");
			return;
		}
		do addIdentifier();
		let s = jt.identifier();
		let s = s.copy();
		let l = jt.getLine();
		do advance();
		do subroutineCall(s, l);
		do s.dispose();
		let z = matchSymbol(C.SEMICOLON(), false);
		if (~z) {	
			do error("';' expected..");
			return;
		}
		do addSymbol();
		do advance();
		do end("doStatement"); 
		return;
	}

	method void callbackCall() {
		var boolean z;
		if (~matchKeyword(C.CALLBACK(), false)) {
			return;
		}
		let z = matchSymbol(C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("'(' expected");
		
		}
		do advance();
		do compileExpression();
		let z = matchSymbol(C.COMMA(), false);
		if (~z) {
			do error("',' expected");
		}
		do advance();
		do compileExpression();
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (~z) {
			do error("')' expected");
		}
		do advance();
		return;
		
	}
	method void subroutineCall(String subroutineName, int tokenLine) {
		var boolean z;
		let z = matchSymbol(C.LEFT_PARENTHESIS(), false);
		if (z) {
			do addSymbol();
			do advance();
			do compileExpressionList();
			let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
			if (~z) {	
				do error("')' expected");
				return;
			}
			do addSymbol();
			do advance();
			return;
		}
		let z = matchSymbol(C.DOT(), false);
		if (~z) {
			do error("'(' or '.' expected");
			return;
		}
		do addSymbol();
		let z = matchIdentifier(true); // subroutineName
		if (~z) {
			do error("subroutineName expected");
			return;
		}
		do addIdentifier();
		let z = matchSymbol(C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("'(' expected");
			return;
		}
		do addSymbol();
		do advance();
		do compileExpressionList();
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (z) {
			do addSymbol();
			do advance();
			return;
		}
		do error("')' expected.");
		return;
	}

	method void compileLet() {
		var boolean z;
		do begin("letStatement");
		if (~matchKeyword(C.LET(), false)) {
			do error("let expected");
			return;
		}

		do addKeyword();
		let z = matchIdentifier(true); 
		if (~z) {
			do error("varName expected");
			return;
		}
		do addIdentifier();
		let z = matchSymbol(C.LEFT_SQUARE_BRACKET(), true);
		if (z) {
			do addSymbol();
			do advance();
			do compileExpression();
			let z = matchSymbol(C.RIGHT_SQUARE_BRACKET(), false);
			if (~z) {	
				do error("']' expected");
				return;
			}
			do addSymbol();
			do advance();
		}
		let z = matchSymbol(C.EQUAL(), false);
		if (~z) {
			do error("'=' expected");
			return;
		}
		do addSymbol();
		do advance();
		do compileExpression();
		let z = matchSymbol(C.SEMICOLON(), false);
		if (~z) {
			do error("';' expected");
			return;
		}
		do addSymbol();
		do advance();
		do end("letStatement");
		return;
	}

	method void compileWhile() {
		var boolean z;
		
		if (~matchKeyword(C.WHILE(), false)) {
			do error("while expected");
			return;
		}
		do begin("whileStatement");
		let z = matchSymbol(C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("'(' expected");
			return;
		}
		do addSymbol();	
		do advance();
		do compileExpression();
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (~z) {
			do error("')' expected");
			return;
		}
		do addSymbol();	
	
		let z = matchSymbol(C.LEFT_CURLY_BRACKET(), true);
		if (~z) {
			do error("'{' expected");
			return;
		}
		do addSymbol();	
		do advance();
		do compileStatements();
		let z = matchSymbol(C.RIGHT_CURLY_BRACKET(), false);
		if (~z) {
			do error("'}' expected,");
			return;
		}
		do addSymbol();	
		do end("whileStatement");
		do advance();
		return;
	}

	method void compileReturn() {
		var boolean z;
		if (~matchKeyword(C.RETURN(), false)) {
			do error("return expected");
			return;
		}
		do begin("returnStatement");
		do addKeyword();
		do advance();

		do callbackCall();

		let z = matchSymbol(C.SEMICOLON(), false);
		if (z) {
			do addSymbol();
			do advance();
			return;
		}
		do compileExpression();
		let z = matchSymbol(C.SEMICOLON(), false);
		if (~z) {
			do error("';' expected...");
			return;
		}
		do addSymbol();
		do end("returnStatement");
		do advance();
		return;
	}

	method void compileIf() {
		var boolean z;
		if (~matchKeyword(C.IF(), false)) {
			do error("if expected");
			return;
		}
		do begin("ifStatement");
		do addKeyword();
		let z = matchSymbol(C.LEFT_PARENTHESIS(), true);
		if (~z) {
			do error("'(' expected");
			return;
		}
		do addSymbol();	
		do advance();
		do compileExpression();
		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (~z) {
			do error("')' expected..");
			return;
		}
		do addSymbol();	
	
		let z = matchSymbol(C.LEFT_CURLY_BRACKET(), true);
		if (~z) {
			do error("'{' expected");
			return;
		}
		do addSymbol();	
		do advance();
		do compileStatements();
		let z = matchSymbol(C.RIGHT_CURLY_BRACKET(), false);
		if (~z) {
			do error("'}' expected.");
			return;
		}
		do addSymbol();	
		let z = matchKeyword(C.ELSE(), true);
		if (~z) {
			do end("ifStatement");
			return;
		}
		do addKeyword();	
		let z = matchSymbol(C.LEFT_CURLY_BRACKET(), true);
		if (~z) {
			do error("'{' expected");
			return;
		}
		do addSymbol();	
		do advance();
		do compileStatements();
		let z = matchSymbol(C.RIGHT_CURLY_BRACKET(), false);
		if (~z) {
			do error("'}' expected..");
			return;
		}
		do addSymbol();	
		do end("ifStatement");
		do advance();
		return;
	}

	method void genOp() {
		var int c;
		let c = jt.symbol();
		if (c = C.PLUS()) {
			return;
		}
		if (c = C.MINUS()) {
			return;
		}
		if (c = C.MULTIPLY()) {
			return;
		}
		if (c = C.DIV()) {
			return;
		}
		if (c = C.AND()) {
			return;
		}
		if (c = C.OR()) {
			return;
		}
		if (c = C.GREATER()) {
			return;
		}
		if (c = C.LESS()) {
			return;
		}
		if (c = C.EQUAL()) { 
			return;
		}
		do error("unexpected op.");
		return;
	}

	method void compileExpression() {
		var boolean z;
		do begin("expression");
		while (true) {
			do compileTerm();
			if (matchSymbol(C.SEMICOLON(), false)) {
				do end("expression");
				return;
			}
			if (matchSymbol(C.RIGHT_PARENTHESIS(), false)) {
				do end("expression");
				return;
			}
			if (matchSymbol(C.RIGHT_SQUARE_BRACKET(), false)) {
				do end("expression");
				return;
			}
			if (matchSymbol(C.COMMA(), false)) {
				do end("expression");
				return;
			}
			if (jt.tokenType() = C.SYMBOL()) {
				do genOp();
				do addSymbol();
				if (~advance()) {
					do error(
						"missing token at end of file");
					return;
				}
			} else {
				do end("expression");
				return;
			}
		}	
		return;
	}

	method void compileTerm() {
		var boolean z;
		var String s;
		var int l;
		do begin("term");
		let z = matchIntegerConstant(false);
		if (z) {
			do addIntegerConstant();
			do end("term");
			do advance();
			return;
		}
		let z = matchStringConstant(false);
		if (z) {
			do addStringConstant();
			do end("term");
			do advance();
			return;
		}
		if (jt.tokenType() = C.KEYWORD()) {
			if (matchKeyword(C.TRUE(), false)) {
				do addKeyword();
				do end("term");
				do advance();
				return;
			} else { if (matchKeyword(C.FALSE(), false)) {
				do addKeyword();
				do end("term");
				do advance();
				return;
			} else { if (matchKeyword(C.NULL(), false)) {
				do addKeyword();
				do end("term");
				do advance();
				return;
			} else { if (matchKeyword(C.THIS(), false)) {
				do addKeyword();
				do end("term");
				do advance();
				return;
			}}}}
		}
		let z = matchSymbol(C.TILDE(), false);
		if (z) {
			do addSymbol();
			do advance();
			do compileTerm();
			do end("term");
			return;
		}
		let z = matchSymbol(C.MINUS(), false);
		if (z) {
			do addSymbol();
			do advance();
			do compileTerm();
			do end("term");
			return;
		}

		let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
		if (z) {
			do end("term");
			return;
		}

		let z = matchSymbol(C.LEFT_PARENTHESIS(), false);
		if (z) {
			do addSymbol();
			do advance();
			do compileExpression();
			let z = matchSymbol(C.RIGHT_PARENTHESIS(), false);
			if (z) {
				do addSymbol();
				do advance();
				do end("term");
				return;
			}
			do error("')' expected here");
			return;
		}
		
		// className varName subroutiN
		let z = matchIdentifier(false); 
		if (~z) {
			do error("Unexpected token...");
			return;
		}
		do addIdentifier();
		let s = jt.identifier();
		let s = s.copy();
		let l = jt.getLine();
		let z = matchSymbol(C.LEFT_SQUARE_BRACKET(), true);
		if (z) {
			do addSymbol();
			do s.dispose();
			do advance();
			do compileExpression();
			let z = matchSymbol(C.RIGHT_SQUARE_BRACKET(), false);
			if (~z) {
				do error("']' expected.");
				return;
			}
			do addSymbol();
			do end("term");
			do advance();
			return;
		}

		let z = matchSymbol(C.DOT(), false);
		let z = z | matchSymbol(C.LEFT_PARENTHESIS(), false);
		if (~z) {
			// match s
			do s.dispose();
			do end("term");
			return;
		}
		do subroutineCall(s, l);
		do s.dispose();
		do end("term");
		return;
	}

	method void compileExpressionList() {
		do begin("expressionList");
		do compileExpression();
		while (matchSymbol(C.COMMA(), false)) {
			do addSymbol();
			do advance();
		 	do compileExpression();
		}
		do end("expressionList");
		return;
	}

}

